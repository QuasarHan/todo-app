plugins {
    id 'java'
    id 'maven-publish' //новый плагин для публикации
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.example'
version = findProperty('version') ?: '0.1.0'
sourceCompatibility = '23'
targetCompatibility = '23'
mainClassName = 'org.example.TodoApp'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveBaseName.set('todo-app')
    archiveClassifier.set('')
    archiveVersion.set(project.version.toString())
    mergeServiceFiles()
}

tasks.jar {
    enabled = false
}

tasks.named('distZip') {
    dependsOn tasks.named('shadowJar')
}
tasks.named('distTar') {
    dependsOn tasks.named('shadowJar')
}
tasks.named('startScripts') {
    dependsOn tasks.named('shadowJar')
}
tasks.named('startShadowScripts') {
    dependsOn tasks.named('jar')
}

tasks.build {
    dependsOn shadowJar
}

publishing{
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.example'
            artifactId = 'todo-app'
            version = project.version

            // публикуем именно fat JAR
            artifact(tasks.shadowJar) {
                classifier = null // имя вида todo-app-0.5.0.jar
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/QuasarHan/todo-app")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }

        }
    }
}